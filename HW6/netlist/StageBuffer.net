* 6-Stage Cascaded CMOS Inverter Chain

.incl tsmc18.sp

* Parameters
.param Wp=0.75u
.param Lp=0.2u
.param Wn=0.5u
.param Lnch=0.2u
.param Cl=10pf
.param m=3.71

* Power supply
VDD vdd 0 1.8

* Input pulse
Vin in 0 PULSE(0 1.8 100p 10p 10p 100n 200n)

* Inverter 1
M1_1 n1 in vdd vdd cmosp W={Wp} L={Lp}
M2_1 n1 in 0 0 cmosn W={Wn} L={Lnch}

* Inverter 2
M1_2 n2 n1 vdd vdd cmosp W={m*Wp} L={Lp}
M2_2 n2 n1 0 0 cmosn W={m*Wn} L={Lnch}

* Inverter 3
M1_3 n3 n2 vdd vdd cmosp W={m**2*Wp} L={Lp}
M2_3 n3 n2 0 0 cmosn W={m**2*Wn} L={Lnch}

* Inverter 4
M1_4 n4 n3 vdd vdd cmosp W={m**3*Wp} L={Lp}
M2_4 n4 n3 0 0 cmosn W={m**3*Wn} L={Lnch}

* Inverter 5
M1_5 n5 n4 vdd vdd cmosp W={m**4*Wp} L={Lp}
M2_5 n5 n4 0 0 cmosn W={m**4*Wn} L={Lnch}

* Inverter 6
M1_6 out n5 vdd vdd cmosp W={m**5*Wp} L={Lp}
M2_6 out n5 0 0 cmosn W={m**5*Wn} L={Lnch}

* Load Capacitance
Cload out 0 {Cl}

* Simulation control
*.tran 0.1n 10n

* Propagation delay measurements (optional)
*.measure tran tplh TRIG V(in) VAL=0.9 RISE=1 TARG V(out) VAL=0.9 RISE=1
*.measure tran tphl TRIG V(in) VAL=0.9 FALL=1 TARG V(out) VAL=0.9 FALL=1

.control
tran 0.1n 7n
plot v(in) v(out)
meas tran tphl TRIG v(in) VAL=0.9 RISE=1 TARG v(out) VAL=0.9 FALL=1
meas tran tplh TRIG v(in) VAL=0.9 FALL=1 TARG v(out) VAL=0.9 RISE=1
.endc
.end
