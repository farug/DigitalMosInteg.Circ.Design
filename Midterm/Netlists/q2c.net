* 3-Stage Cascaded CMOS Inverter Chain

.incl tsmc18.sp

* Parameters
.param Wp=0.75u
.param Lp=0.2u
.param Wn=0.5u
.param Lnch=0.2u
.param Cl=5pf

* Power supply
VDD vdd 0 1.8

* Input pulse
Vin in 0 PULSE(0 1.8 100p 10p 10p 100n 200n)

* Inverter 1
M1_1 n1 in vdd vdd cmosp W={Wp} L={Lp} ad='Wp*0.55u' as='Wp*0.55u' pd='2*(Wp+0.55u)' ps='2*(Wp+0.55u)'
M2_1 n1 in 0 0 cmosn W={Wn} L={Lnch} ad='Wn*0.55u' as='Wn*0.55u' pd='2*(Wn+0.55u)' ps='2*(Wn+0.55u)'

.param wp2=8.13u
.param wn2=5.42u
* Inverter 2
M1_2 n2 n1 vdd vdd cmosp W={wp2} L={Lp} ad='wp2*0.55u' as='wp2*0.55u' pd='2*(wp2+0.55u)' ps='2*(wp2+0.55u)'
M2_2 n2 n1 0 0 cmosn W={wn2} L={Lnch} ad='wn2*0.55u' as='wn2*0.55u' pd='2*(wn2+0.55u)' ps='2*(wn2+0.55u)'

.param wp3=88.3u
.param wn3=58.86u
* Inverter 3
M1_3 out n2 vdd vdd cmosp W={wp3} L={Lp} ad='wp3*0.55u' as='wp3*0.55u' pd='2*(wp3+0.55u)' ps='2*(wp3+0.55u)'
M2_3 out n2 0 0 cmosn W={wn3} L={Lnch} ad='wn3*0.55u' as='wn3*0.55u' pd='2*(wn3+0.55u)' ps='2*(wn3+0.55u)'



* Load Capacitance
Cload out 0 {Cl}

* Simulation control
*.tran 5p 200n

* Propagation delay measurements (optional)
*.measure tran tplh TRIG V(in) VAL=0.9 RISE=1 TARG V(out) VAL=0.9 RISE=1
*.measure tran tphl TRIG V(in) VAL=0.9 FALL=1 TARG V(out) VAL=0.9 FALL=1

.control
tran 5p 200n
plot v(in) v(out)
meas tran tphl TRIG v(in) VAL=0.9 RISE=1 TARG v(out) VAL=0.9 FALL=1
meas tran tplh TRIG v(in) VAL=0.9 FALL=1 TARG v(out) VAL=0.9 RISE=1

.endc
.end